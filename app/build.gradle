apply plugin: 'com.android.application'
apply plugin: 'android-apt'
//集成fir.im发布
apply plugin:'im.fir.plugin.gradle'

fir{
    //必填 上传 fir.im apk 字段，否则无法上传 APP 到 fir.im
    apiToken getLocalProperty("FIR_TOKEN")
    //可选 上传fir.im 中显示的changelog
    changeLog getLocalProperty("UPDATE_LOG")
}

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.blakequ.androidblemanager"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "FIR_TOKEN", getLocalProperty("FIR_TOKEN_STRING")
        //设置输出app默认前缀名称
        //project.ext.set("archivesBaseName", "BleManager_v" + versionName + "_"+today);
    }

    //签名配置
    signingConfigs {
        release {
            storeFile file('blakequ.jks')
            //this is for gradle.properties
//            storePassword project.hasProperty("KEYSTORE_PASS") ? KEYSTORE_PASS : System.getenv("KEYSTORE_PASS")
//            keyAlias project.hasProperty("ALIAS_NAME") ? ALIAS_NAME : System.getenv("ALIAS_NAME")
//            keyPassword project.hasProperty("ALIAS_PASS") ? ALIAS_PASS : System.getenv("ALIAS_PASS")
            //this is for local.properties
            storePassword getLocalProperty("KEYSTORE_PASS") ? getLocalProperty("KEYSTORE_PASS") : System.getenv("KEYSTORE_PASS")
            keyAlias getLocalProperty("ALIAS_NAME") ? getLocalProperty("ALIAS_NAME") : System.getenv("ALIAS_NAME")
            keyPassword getLocalProperty("ALIAS_PASS") ? getLocalProperty("ALIAS_PASS") : System.getenv("ALIAS_PASS")
        }
    }

    buildTypes {
        debug {
            //对齐优化设置为true，否则找不到该task (默认不写也是true)
            zipAlignEnabled true
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            //自定义apk安装包名
            applicationVariants.all { variant ->
                variant.mergedFlavor.versionCode = gitVersionCode()
                variant.mergedFlavor.versionName = gitVersionTag()
                variant.outputs.each { output ->
                    output.outputFile = new File(
                            output.outputFile.parent + "/${variant.buildType.name}",
                            "androidblemanager-${variant.buildType.name}-${variant.versionName}-${releaseTime()}.apk".toLowerCase())
                }
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

repositories {
    maven {
        url "https://repo.commonsware.com.s3.amazonaws.com"
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.commonsware.cwac:merge:1.1.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.github.hotchemi:permissionsdispatcher:2.1.3'
    apt 'com.github.hotchemi:permissionsdispatcher-processor:2.1.3'

//    compile project(':bluetooth-manager-lib')
    compile 'com.blakequ.androidblemanager:bluetooth-manager-lib:1.0.3'
}
