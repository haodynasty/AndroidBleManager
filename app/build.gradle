apply plugin: 'com.android.application'
//集成fir.im发布
//apply plugin:'im.fir.plugin.gradle'

//fir{
//    //必填 上传 fir.im apk 字段，否则无法上传 APP 到 fir.im
//    apiToken hasLocalProperty("FIR_TOKEN") ? getLocalProperty("FIR_TOKEN") : System.getenv("FIR_TOKEN")
//    //可选 上传fir.im 中显示的changelog
//    changeLog gitLogFirstCommit()//getLocalProperty("UPDATE_LOG")
//}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.blakequ.androidblemanager"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode 2
        versionName "1.1"
        //设置输出app默认前缀名称
//        project.ext.set("archivesBaseName", "BleManager_v" + versionName + "_"+today);
    }

    //签名配置
    signingConfigs {
      def appStoreFile = System.getenv("STORE_SIGN_FILE")
      def appStorePassword = System.getenv("STORE_SIGN_PWD")
      def appKeyAlias = System.getenv("KEY_ALIAS")
      def appKeyPassword = System.getenv("KEY_PASSWORD")
      //当不能从环境变量里获取到签名信息的时候，就使用项目中带的debug签名
      if(!appStoreFile||!appStorePassword||!appKeyAlias||!appKeyPassword){
        appStoreFile = "debug.keystore"
        appStorePassword = "android"
        appKeyAlias = "androiddebugkey"
        appKeyPassword = "android"
      }
      release {
        storeFile file(appStoreFile)
        storePassword appStorePassword
        keyAlias appKeyAlias
        keyPassword appKeyPassword
      }
    }

    buildTypes {
        debug {
            //对齐优化设置为true，否则找不到该task (默认不写也是true)
            zipAlignEnabled true
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            //自定义apk安装包名
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    if (variant.buildType.name == 'release') {
                        def fileName = "ble-${gitVersionTag()}-${gitVersionCode()}-${releaseTime()}-${variant.buildType.name}.apk".toLowerCase()
                        outputFileName = fileName
                    }
                }
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.jakewharton:butterknife:7.0.1'
    annotationProcessor 'com.jakewharton:butterknife:7.0.1'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'com.github.hotchemi:permissionsdispatcher:2.1.3'
    annotationProcessor  'com.github.hotchemi:permissionsdispatcher-processor:2.1.3'
    implementation 'com.blakequ.logger:logger:1.0.1'
    implementation project(':bluetooth-manager-lib')
//    compile 'com.blakequ.androidblemanager:bluetooth-manager-lib:latest.integration'
}
